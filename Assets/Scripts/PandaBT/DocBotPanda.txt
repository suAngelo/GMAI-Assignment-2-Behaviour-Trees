tree("Root")
	sequence
		tree("T_IdleState") // Entry/Default State

// Check if each position tag is readable by pandabt
// Meant for testing purposes
tree("T_MoveCheck")
	sequence
		MoveTo("IdlePoint")
		MoveTo("DiagnosisPoint")
		MoveTo("SoftRepairPoint")
		MoveTo("HardRepairPoint")
		MoveTo("RestockingPoint")
		MoveTo("UpdatePoint")

tree("T_IdleState")
	sequence
		MoveTo("IdlePoint") // Note that IdlePoint is also the "Serving Point"
		sequence
			CheckCustomer()
			tree("T_ServingState")
			
tree("T_ServingState")
	sequence
		Serving()
		tree("T_DiagnosisState")

tree("T_DiagnosisState")
	sequence
		ConveyorMoveTo("Diagnosis")
		MoveTo("DiagnosisPoint")
		sequence
			fallback
				sequence
					CheckIfFunctional()
					IndicateYellow("DiagnosisInterface")
					Wait 3.0 // Add a short delay to show the colour change
				IndicateRed("DiagnosisInterface")
				ConveyorMoveTo("PatientSpawn")
				tree("T_DischargeState")
			fallback
				sequence
					CheckIfRepairable()
					IndicateGreen("DiagnosisInterface")
					tree("T_SoftRepairState")
				IndicateRed("DiagnosisInterface")
				ConveyorMoveTo("PatientSpawn")
				// switch to failure

tree("T_SoftRepairState")
	sequence
		ConveyorMoveTo("SoftRepair2")
		MoveTo("SoftRepairPoint")
		fallback
			while
				LocalErrorsNotMaxed()
				sequence
					AttemptRepair()
					IndicateGreen("SoftRepairInterface")
					tree("T_HardRepairState")
			sequence
				IndicateRed("SoftRepairInterface")
				ConveyorMoveTo("PatientSpawn")
				IncrementUniversalErrors()
				tree("T_DischargeState")

tree("T_HardRepairState")
	sequence
		ConveyorMoveTo("HardRepair2")
		MoveTo("HardRepairPoint")
		fallback
			while
				LocalErrorsNotMaxed()
				sequence
					AttemptRepair()
					CreateMess()
					IndicateGreen("HardRepairInterface")
					tree("T_DischargeState")
			sequence
				IndicateRed("HardRepairInterface")
				ConveyorMoveTo("PatientSpawn")
				IncrementUniversalErrors()
				tree("T_DischargeState")	

tree("T_DischargeState")
	sequence
		sequence
			ConveyorMoveTo("PatientSpawn")
			MoveTo("IdlePoint")
		sequence
			DischargeCustomer()
		fallback
			sequence
				UniversalErrorsNotMaxed()
				tree("T_CleanupState")

tree("T_CleanupState")
	sequence
		MoveTo("HardRepairPoint")
		CleanMess()
		// do not switch to idle state as it creates a circular tree
	
	


			
				
				
	

